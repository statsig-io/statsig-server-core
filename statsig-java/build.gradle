import org.gradle.jvm.tasks.Jar
import org.gradle.api.attributes.Category
import org.gradle.api.attributes.Bundling
import org.gradle.api.attributes.Usage

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'io.github.gradle-nexus.publish-plugin' version "1.3.0" apply false
}

if (project == rootProject) {
    apply plugin: 'io.github.gradle-nexus.publish-plugin'
    nexusPublishing {
        repositories {
            sonatype {
                nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
                snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
                username = System.getenv("ORG_GRADLE_PROJECT_MAVEN_USERNAME")
                password = System.getenv("ORG_GRADLE_PROJECT_MAVEN_PASSWORD")
            }
        }
    }
}

group 'com.statsig'
version project.property("version")

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat()
        removeUnusedImports()
        importOrder()
    }
}

java {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

tasks.withType(JavaCompile).configureEach {
    options.release = 8
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    api platform('com.fasterxml.jackson:jackson-bom:2.16.1')
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'

    implementation 'org.jetbrains:annotations:24.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'com.squareup.okhttp3:okhttp:4.9.3'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.3'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}

if (!project.ext.has('artifactTasks')) {
    project.ext.artifactTasks = []
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn tasks.javadoc
    archiveClassifier.set('javadoc')
    from tasks.javadoc.destinationDir
}

// So that javaCore jar will not contain any natives
tasks.named('jar', Jar) {
    exclude 'native/**'
    exclude '**/*.so', '**/*.dylib', '**/*.dll'
}

// To make kong works
tasks.register('jarDev', Jar) {
  archiveClassifier.set('dev')
  from(sourceSets.main.output)
  from('src/main/resources/native') { into('native') }
}

configurations {
  devJar {
    canBeConsumed = true
    canBeResolved = false
    attributes {
      attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
      attribute(Usage.USAGE_ATTRIBUTE,     objects.named(Usage, Usage.JAVA_RUNTIME))
      attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
    }
  }
}

artifacts {
  add('devJar', tasks.named('jarDev'))
}

javadoc {
    options {
        windowTitle = "Statsig Java SDK"
        docTitle = "Statsig Java SDK API"
    }
}

ext.createStatsigJar = { String nameParam, String classifierParam ->
    if (nameParam == "uber") {
        tasks.register("uberJar", Jar) {
            archiveBaseName.set(nameParam)
            archiveClassifier.set("uber")
            from(sourceSets.main.output) {
                include 'com/statsig/**'
                include 'statsigsdk.properties'
            }

            // Include native libraries for all supported platforms
            def platforms = [
                "aarch64-apple-darwin" : "libstatsig_ffi.dylib",
                "x86_64-apple-darwin"  : "libstatsig_ffi.dylib",
                "aarch64-unknown-linux-gnu": "libstatsig_ffi.so",
                "x86_64-unknown-linux-gnu": "libstatsig_ffi.so",
                "x86_64-pc-windows-msvc"   : "statsig_ffi.dll"
            ]

            platforms.each { classifier, libName ->
                def nativeSourceDir = file("src/main/resources/native/${classifier}")
                def nativeLibFile = new File(nativeSourceDir, libName)
                if (nativeLibFile.exists()) {
                    println "Including ${nativeLibFile.absolutePath} as native/${classifier}/${libName}"
                } else {
                    println "⚠️ Missing native lib: ${nativeLibFile.absolutePath}"
                }

                from(nativeSourceDir) {
                    include libName
                    into("native/${classifier}/")
                }
            }

            project.ext.artifactTasks.add([taskName: "uberJar", classifier: "uber"])
        }
        return
    }

    String taskName = nameParam + "Jar"

    tasks.register(taskName, Jar) {
        archiveBaseName.set(nameParam)
        if (nameParam != "java-core") {
            from sourceSets.main.output
        }

        def nativeLib = null
        if (classifierParam) {
            switch (classifierParam) {
                case ~/.*apple.*/:
                    nativeLib = "libstatsig_ffi.dylib"
                    break
                case ~/.*windows.*/:
                    nativeLib = "statsig_ffi.dll"
                    break
                case ~/.*linux.*/:
                    nativeLib = "libstatsig_ffi.so"
                    break
                default:
                    nativeLib = null
            }
        }

        // Include native library if needed
        if (nativeLib) {
            def nativeSourceDir = file("src/main/resources/native/${classifierParam}")
            from(nativeSourceDir) {
                into("native/${classifierParam}/")
            }
            include nativeLib
        }

        if (nameParam == "java-core") {
            from(sourceSets.main.output) {
                include 'com/statsig/**'
                include 'statsigsdk.properties'
            }
        }

        if (classifierParam != null) {
            archiveClassifier.set(classifierParam)
        }

        project.ext.artifactTasks.add([taskName: taskName, classifier: classifierParam])
    }
}

createStatsigJar('uber', 'uber')
createStatsigJar('java-core', null)
// Apple
createStatsigJar('aarch64-apple-darwin', 'aarch64-apple-darwin')
createStatsigJar('x86_64-apple-darwin', 'x86_64-apple-darwin')
// Linux GNU
createStatsigJar('aarch64-unknown-linux-gnu', 'aarch64-unknown-linux-gnu')
createStatsigJar('x86_64-unknown-linux-gnu', 'x86_64-unknown-linux-gnu')
// Linux MUSL
createStatsigJar('x86_64-unknown-linux-musl', 'x86_64-unknown-linux-musl')
createStatsigJar('aarch64-unknown-linux-musl', 'aarch64-unknown-linux-musl')
// Windows
createStatsigJar('x86_64-pc-windows-msvc', 'x86_64-pc-windows-msvc')
createStatsigJar('i686-pc-windows-msvc', 'i686-pc-windows-msvc')
// todo: add support for these platforms
// createStatsigJar('aarch64-pc-windows-msvc', 'windows-arm64')

def expectedTasks = [
    'java-coreJar',
    'uberJar',

    // Apple
    'aarch64-apple-darwinJar',
    'x86_64-apple-darwinJar',

    // Linux GNU
    'aarch64-unknown-linux-gnuJar',
    'x86_64-unknown-linux-gnuJar',

    // Linux MUSL
    'x86_64-unknown-linux-muslJar',
    'aarch64-unknown-linux-muslJar',

    // Windows
    'x86_64-pc-windows-msvcJar',
    'i686-pc-windows-msvcJar',
]

processResources {
    filesMatching('statsigsdk.properties') {
        expand(project.properties)
    }
}

tasks.register('validateTasks') {
    doLast {
        def missingTasks = expectedTasks.findAll { taskName ->
            !project.tasks.findByName(taskName)
        }
        if (!missingTasks.isEmpty()) {
            throw new GradleException("The following expected tasks were not registered: ${missingTasks.join(', ')}")
        }
    }
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.matching { it.name.startsWith("publish") }.all { publishTask ->
    publishTask.dependsOn validateTasks
}

publishing {
    publications {
        create('mavenJava', MavenPublication) {
            artifactId = 'javacore'
            from components.java

            artifact sourcesJar
            artifact javadocJar

            project.ext.artifactTasks.each { e ->
                def t = tasks.named(e.taskName).get()
                if (t.name == 'jar') {
                    return
                }
                if (t.hasProperty('archiveClassifier') && !t.archiveClassifier.getOrNull()) {
                    t.archiveClassifier.set(e.classifier ?: t.name)
                }
                artifact(t)
            }

            pom {
                name.set("Statsig Java Core SDK")
                description.set("A feature gating and a/b testing java library for statsig")
                url.set("https://github.com/statsig-io/statsig-server-core")

                licenses {
                    license {
                        name.set("ISC License (ISC)")
                        url.set("https://github.com/statsig-io/java-server-sdk/blob/main/LICENSE")
                        distribution.set("repo")
                    }
                }

                developers {
                    developer {
                        id.set("statsig")
                        name.set("statsig")
                        url.set("https://github.com/statsig-io/")
                        email.set("support@statsig.com")
                    }
                }

                scm {
                    connection.set("scm:git:https://github.com/statsig/statsig-sdk.git")
                    developerConnection.set("scm:git:ssh://git@github.com:statsig/statsig-sdk.git")
                    url.set("https://github.com/statsig/statsig-sdk")
                }
            }
        }
    }

    repositories {
        maven {
            name = 'mavenCentral'
            url = uri('https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/')
            credentials {
                username = System.getenv('ORG_GRADLE_PROJECT_MAVEN_USERNAME')
                password = System.getenv('ORG_GRADLE_PROJECT_MAVEN_PASSWORD')
            }
        }
    }
}

signing {
    def signingKeyId = System.getenv("ORG_GRADLE_PROJECT_SIGNING_KEY_ID") ?: ""
    def signingKey = System.getenv("ORG_GRADLE_PROJECT_SIGNING_KEY") ?: ""
    def signingPassword = System.getenv("ORG_GRADLE_PROJECT_SIGNING_PASSWORD") ?: ""
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    sign publishing.publications['mavenJava']
}

checkstyle {
    toolVersion = "9.2"
    configFile = rootProject.file("config/checkstyle.xml")
    ignoreFailures = true // TODO we need change to false in the future
}
